// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // if (inc[t] == 1)...
    Inc16(in=lastOut, out=incOut);
    Mux16(a=lastOut, b=incOut, sel=inc, out=lastOrInc);
    // if (load[t] == 1)...
    Mux16(a=lastOrInc, b=in, sel=load, out=loadOrElse);
    // if (reset[t] == 1)...
    Mux16(a=loadOrElse, b[0..15]=false, sel=reset, out=resetOrElse);

    // determine if we should load a new value into memory
    Or(a=inc, b=load, out=incOrLoad);
    Or(a=incOrLoad, b=reset, out=shouldLoad);

    Register(in=resetOrElse, load=shouldLoad, out=lastOut, out=out);
}
